
// This file is part of colorful-home, a nice user experience for touchscreens.
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
//
// Copyright (c) 2011, Tom Swindell <t.swindell@rubyx.co.uk>
// Copyright (c) 2012, Timur Krist√≥f <venemo@fedoraproject.org>

import QtQuick 1.1

MouseArea {
    property alias iconFilePath: iconImage.source
    property alias iconCaption: iconText.text

    // Application icon for the launcher
    Image {
        id: iconImage
        anchors {
            horizontalCenter: parent.horizontalCenter
            top: parent.top
            topMargin: 8
        }
        width: 80
        height: width
        asynchronous: true
        onStatusChanged: {
            if (status === Image.Error) {
                console.log("Error loading an app icon, falling back to default.");
                iconImage.source = ":/images/icons/apps.png";
            }
        }

        Connections {
            target: model.object
            onIsLaunchingChanged: {
                if (model.object.isLaunching) {
                    launchAnimationStage1.start()
                }
                else {
                    if (launchAnimationStage1.running)
                        launchAnimationStage1.stop();
                    if (launchAnimationStage2.running)
                        launchAnimationStage2.stop();

                    iconImage.scale = 1.0;
                }
            }
        }
        NumberAnimation {
            id: launchAnimationStage1
            target: parent
            property: "scale"
            from: 1
            to: 0.6
            duration: 700
            onCompleted: {
                if (model.object.isLaunching)
                    launchAnimationStage2.start();
                else
                    iconImage.scale = 1.0;
            }
        }
        NumberAnimation {
            id: launchAnimationStage2
            target: parent
            property: "scale"
            from: 0.6
            to: 1
            duration: 700
            onCompleted: {
                if (model.object.isLaunching)
                    launchAnimationStage1.start();
            }
        }
    }

    // Caption for the icon
    Text {
        id: iconText
        // elide only works if an explicit width is set
        width: parent.width
        elide: Text.ElideRight
        horizontalAlignment: Text.AlignHCenter
        font.pixelSize: 20
        color: 'white'
        anchors {
            left: parent.left
            right: parent.right
            top: iconImage.bottom
            topMargin: 5
        }
    }
}
